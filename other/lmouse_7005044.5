

;;;THE FOLLOWING IS THE LISP CODE FOR REAL TIME LOGO
;;;THE BASIC IDEA IS THAT AS SOON AS A LETTER IS TYPED
;;;THE TURTLE MOVES AND THE LOGO CODE CORRESPONDING TO
;;;THE MOVEMENTS AND PENSTATE ARE SAVED UNTIL TAUGHT
;;;VIA THE LETTER "T" WHICH GENERATES A LOGO PROCEDURE
;;;SLIGHTLY OPTIMIZED AND PARAMATERIZED BY :SIZE, THE
;;;SIZE OF THE STEPS ON "FORWARD" OR "BACK".  THE INPUT TO
;;;NEWLY DEFINED PROCEDURE IS NORMALIZED TO 1., I.E.
;;;IF CALLED WITH 2. IT WILL DRAW THE THING TWICE AS
;;;BIG AS ORIGINALLY DEFINED, WITH .5 HALF AS BIG.
;;;A SINGLE INFIX PARAMETER IS OPTIONAL. THIS INTEGER
;;;PARAMETER IS CALLED "NUMBER" AND IF USED BY FORWARD,BACK,RT, AND LT
;;;TO CHANGE ITS INPUT AND DEFUALT INPUT SIMULTANEOUSLY.
;;;    ANOTHER IMPORTANT FEATURE IS THAT RUBOUT WILL ERASE
;;;THE LAST COMMAND FROM THE DISPLAY AND PROGRAM BUFFER,
;;;WHICH IS CALLED "PROGRAM". ANOTHER USEFUL FEATURE IS THAT
;;;IF THE OPTIONAL PARAMETER IS EITHER PRECEDED OR FOLLOWED
;;;BY A "#" THEN IT WILL BE INTERPRETED AS DO THE FOLLOWING
;;;"NUMBER" NUMBER OF TIMES.  
;;;    THE SYSTEM PROVIDES AN AUTOSAVE FEATURE WHICH SAVES
;;;	1. ALL LOGO PROCEDURES
;;;	2. ALL DEFINITIONS OF NEW COMMANDS
;;;	3. VARIOUS INTERNAL STATES
;;;ALSO WHEN EXITING VIA "Q" ONE CAN SAVE THE ABOVE.
;;;IF FOR ANY REASON YOU ARE THROWN OUT OF "SM", YOU
;;;CAN RETURN IF THE DISPLAY IS STILL OK BY CALLING SM
;;;WITH T AS AN ARGUEMENT
;;;THE ABILITY TO INTERACTIVELY DEFINE NEW SINGLE LETTER
;;;COMMANDS IS PROVIDED BY "N" THE CODE PUT ON THE NEW
;;;LETTER'S PROPERTY LIST IS EVAL-ED TWICE THE FIRST TIME
;;;TO PRODUCE A FIXED INSTUCTION STATEMENT THE SECOND TIME
;;;TO EXECUTE IT AND SAVE IT.  SO IF ONE DOES NOT WANT THE
;;;NEW COMMAND TO BE PUT IN THE PROGRAM BUFFER (AS "HT" AND
;;;"ST") THEN THE FIRST EVALUATION SHOULD PERFORM THE ACTION
;;;AND SHOULD RETURN EITHER NIL OR "?"
;;;ALSO INFIX ARGUEMENTS PUT THEMSELVES ON THE LETTER COMMANDS
;;;PROPERY LIST UNDER THE INDICATOR "DEFAULT".

(DECLARE (OR (STATUS FEATURE DEFINE) (FASLOAD DEFINE FASL AI LLOGO)) (READ)) 

(OR (STATUS FEATURE DEFINE) (FASLOAD DEFINE FASL AI LLOGO)) 

(DECLARE (*LEXPR STARTDISPLAY LOGO-PRINT)
	 (SPECIAL NUMBER PROGRAM FACTOR :EDITMODE REPEAT? :PENSTATE
		  FUNCTIONOPPOSITE PROCEDURESTAUGHT NEWCOMMANDS LASTPROGRAM
		  AUTOSAVEFILE AUTOSAVESECONDS)) 

;;; NUMBER IS THE OPTIONAL INFIX ARGUEMENT IS 0. IF NONE GIVEN
;;; PROGRAM IS THE PROGRAM BUFFER WHERE THE LOGO CODE IS KEPT
;;; NOTE SINCE PROGRAM IS GLOBAL RECURSING SM WILL DO NO GOOD
;;; FACTOR IS -1 IF A "-" WERE GIVEN BEFORE THE INFIX OPERATOR
;;; REPEAT? IS T IF A "#" WAS TYPED
;;; FUNCTIONOPPOSITE IS AN A-LIST ON FUNCTION AND ITS UN-DOER
;;; PROCEDURESTAUGHT IS A LIST OF THE PROCEDURES DEFINED BY "T"
;;; NEWCOMMANDS IS A LIST OF USER DEFINED SINGLE LETTER COMMANDS
;;; AUTOSAVEFILE IS THE FILE NAME WHERE THE "WORKSPACE" IS SAVED
;;; AUTOSAVESECONDS IS THE NUMBER OF SECONDS BETWEEN AUTO SAVING

(OR (BOUNDP 'PROCEDURESTAUGHT) (SETQ PROCEDURESTAUGHT NIL NEWCOMMANDS NIL)) 

(SSTATUS FEATURE LMOUSE)

(OR (STATUS FEATURE TVRTLE) (FASLOAD TVRTLE FASL AI LLOGO)) 

(COND ((STATUS FEATURE LLOGO)
       (MAPC '(LAMBDA (ATOM) (OBTERN ATOM LOGO-OBARRAY))
             '(H S F B R L U D C A P G W T N X Q AUTOSAVE UNION)))
      ((DEFPROP REQUEST READ EXPR)
       (DEFPROP TYPEIN READ EXPR))) 

;;;THIS STARTS THE MOUSE UP
;;;IT READS IN SAVED "WORKSPACES" FROM PREVIOUS SESSIONS
;;;IT STARTS THE DISPLAY
;;;IT SETS THE ALARMCLOCK FOR AUTOSAVING
;;;IT STARTS UP THE PROCESS READLETTER LOOP
;;;AN OPTIONAL ARGUEMENT MEANS RETURNING TO MOUSE

;;READ AND PRINT FUNCTIONS.

(DEFUN TOP-SCREEN-TYPE ARGS
       (CURSORPOS 'T)
       (CURSORPOS '/])
       (DO ((I 1. (1+ I))) ((> I ARGS) (ARG (1- I)) (TERPRI)) (PRINC (ARG I))))


(DEFUN ACTIVATE-LETTERS NIL 
       (APPLY 'SSTATUS
	      (CONS 'TTY
		    ((LAMBDA (TTYST1 TTYST2) 
			     (LIST (BOOLE 7. 33554432. TTYST1) TTYST2))
		     (CAR (STATUS TTY))
		     (CADR (STATUS TTY)))))) 


;;THIS READS LETTERS AND SETS NUMBER, FACTOR & REPEAT?
;;IT ALSO CALLS THE RUBOUT ROUTINE
;;IT CONVERTS LOWER CASE TO UPPER

(DEFUN READLETTER NIL 
       ((LAMBDA (NCHAR) 
                ;;LOWER CASE TO UPPER.
		(AND (< NCHAR 123.) (> NCHAR 96.) (SETQ NCHAR (- NCHAR 32.)))
		(COND ((= NCHAR 127.)
                       ;;RUBOUT.
		       (COND ((= NUMBER 0.) (UNDO-IT) (READLETTER))
			     (T (SETQ NUMBER (// NUMBER 10.))
				(RUBOUT)
				(READLETTER))))
                      ;;NUMBERS.
		      ((AND (> NCHAR 47.) (< NCHAR 58.))
		       (COND ((= FACTOR 1.)
			      (SETQ NUMBER (+ (* NUMBER 10.) (- NCHAR 48.))))
			     (T (SETQ NUMBER (- (* NUMBER 10.) (- NCHAR 48.)))))
		       (READLETTER))
		      ((= NCHAR 45.) (SETQ FACTOR -1.) (READLETTER))
		      ((= NCHAR 35.) (SETQ REPEAT? T) (READLETTER))
		      (T (ASCII NCHAR))))
	(TYI)))

;;;SHOULD MOVE THE CURSOR BACK FOR RUBOUT IN INPUTING NUMBERS

(DEFUN RUBOUT NIL 
       ((LAMBDA (L C) (COND ((= C 0.) NIL)
			    (T (CURSORPOS L (1- C))
			       (PRINC '/ )
			       (CURSORPOS L (1- C))
			       (TYO 127.))))
	(CAR (CURSORPOS))
	(CDR (CURSORPOS))))

;;*PAGE



(DEFINE SM N 
 (ACTIVATE-LETTERS)
 (CATCH
  ((LAMBDA (FACTOR NUMBER) 
    (COND ((= N 0.)
	   (TOP-SCREEN-TYPE '"WELCOME TO REAL TIME LOGO")
	   (SET-ALARM?)
	   (SETQ PROGRAM NIL)
	   (TOP-SCREEN-TYPE '"DO YOU WANT A MOUSE FILE READ IN?")
	   (AND (EQ (READLETTER) 'Y)
		(TOP-SCREEN-TYPE '"WHAT IS THE FILENAME?")
		((LAMBDA (FILENAME) 
			 (COND ((APPLY 'UPROBE FILENAME)
				(APPLY 'RF FILENAME))
			       (T (TOP-SCREEN-TYPE FILENAME '" NOT FOUND"))))
		 (REQUEST)))
	   (STARTDISPLAY)
	   (TOP-SCREEN-TYPE '"TYPE ?? FOR HELP"))
	  (T (ALARMCLOCK 'TIME AUTOSAVESECONDS)
	     (TOP-SCREEN-TYPE '"WELCOME BACK")))
    (DO ((LETTER (READLETTER) (READLETTER)) (:EDITMODE NIL) (REPEAT? NIL))
	(NIL NIL)
	(OR (ERRSET (PROCESS LETTER))
	    (TOP-SCREEN-TYPE '"LOST IN TRYING TO PROCESS " LETTER))
	    (SETQ NUMBER 0.)
	    (SETQ FACTOR 1.)
    (SETQ REPEAT? NIL)))
   1.
   0.)
  OUT-OF-SM)) 

;;;PROCESSES THE LETTER BY DOUBLE EVALULATION OF
;;;THE LETTER'S "LISPCODE" INDICATOR AND
;;;SAVES AWAY CODE IN "PROGRAM"

(DEFUN PROCESS (LETTER) 
       ((LAMBDA (LETTERS-CODE) 
	 (COND
	  (LETTERS-CODE
	   (OR (= NUMBER 0.) REPEAT? (PUTPROP LETTER NUMBER 'DEFAULT))
	   ((LAMBDA (VALUE-OF-LETTER BEGINSTATE) 
		    (AND VALUE-OF-LETTER
			 (OR (NOT (= FACTOR -1.))
			     (SETQ VALUE-OF-LETTER
				   (SUBST (OPPOSITE-FUNCTION (CAR VALUE-OF-LETTER))
					  (CAR VALUE-OF-LETTER)
					  VALUE-OF-LETTER))
			     T)
			 (OR (NOT REPEAT?)
			     (SETQ VALUE-OF-LETTER (LIST 'REPEAT
							 (LIST 'QUOTE
							       VALUE-OF-LETTER)
							 NUMBER)))
			 (COND ((EQ VALUE-OF-LETTER '?))
			       (T (PRINC '/ / / / / )
				  (TOP-SCREEN-TYPE VALUE-OF-LETTER)
				  (EVAL VALUE-OF-LETTER)
				  (SETQ PROGRAM (CONS (CONS VALUE-OF-LETTER
							    BEGINSTATE)
						      PROGRAM))))))
	    (EVAL LETTERS-CODE)
	    (HERE)))
	  (T (TYO 7.))))
	(GET LETTER 'LISPCODE))) 

;;;PUTS ERASER DOWN AND RUNS THE OPPOSITE OF THE
;;;MOST RECENT COMMAND IF IT CAN OTHERWISE
;;;RERUNS PROGRAM BUFFER 
;;;UPDATES PROGRAM BUFFER  

(DEFUN UNDO-IT NIL 
       ((LAMBDA (PENSTATE ERASERSTATE FUNCTION) 
	 ((LAMBDA (OPPOSITE-FUNCTION) 
	   (COND (PROGRAM (COND (:PENSTATE (ERASERDOWN)) (:ERASERSTATE (PENDOWN)))
                          ;;IF PEN WAS DOWN, LOWER ERASER. IF ERASER, THEN PEN.
			  (TOP-SCREEN-TYPE '- (CAAR PROGRAM))
			  (COND (OPPOSITE-FUNCTION
                                 (APPLY OPPOSITE-FUNCTION (CDAAR PROGRAM)))
				(T (PENUP)
				   (SETTURTLE (CDR (CAR PROGRAM)))
				   (ERASERDOWN)
				   (EVAL (CAAR PROGRAM))
				   (ERASERUP)
				   (PENUP)
				   (SETTURTLE (CDR (CAR PROGRAM)))))
			   (cond (PENSTATE (PENDOWN))
				 (t (penup)))
			  (SETQ PROGRAM (CDR PROGRAM)))
		 (T (TYO 7.))))
	  (OPPOSITE-FUNCTION FUNCTION)))
	:PENSTATE
        :ERASERSTATE
	(COND ((EQ (CAAAR PROGRAM) 'REPEAT) (CAR (CADR (CAAR PROGRAM))))
	      (T (CAAAR PROGRAM))))) 

;;;TRIES TO FIND THE OPPOSITE OF A FUNCTION

(DEFUN OPPOSITE-FUNCTION (LOGO-FUNCTION) (CDR (ASSQ LOGO-FUNCTION FUNCTIONOPPOSITE))) 

(SETQ FUNCTIONOPPOSITE '((FORWARD . BACK)
			 (BACK . FORWARD)
			 (RIGHT . LEFT)
			 (LEFT . RIGHT)
			 (ST . HT)
			 (HT . ST)
			 (PENUP . PENDOWN)
			 (PENDOWN . PENUP))) 

(DEFPROP Q (EXIT-MOUSE) LISPCODE) 

(DEFPROP Q "QUIT OUT OF MOUSE MODE" STORY) 

(DEFUN EXIT-MOUSE NIL 
       (ALARMCLOCK 'TIME -1.)
       (TOP-SCREEN-TYPE '"DO YOU WANT TO SAVE YOUR WORK?")
       (AND (EQ (READLETTER) 'Y)
	    (TOP-SCREEN-TYPE '"WHAT FILENAME?")
	    ((LAMBDA (FNAME) (APPLY 'SAVE FNAME) (SAVE-NEW-COMMANDS FNAME))
	     (REQUEST)))
       (THROW 'OUT-OF-MOUSE OUT-OF-SM)) 

;;;NOTE THAT HT NOR ST ARE PUT IN PROGRAM BUFFER

(DEFPROP H (HT) LISPCODE) 

(DEFPROP H "HIDES THE TURTLE" STORY) 

(DEFPROP S (ST) LISPCODE) 

(DEFPROP S "SHOWS THE TURTLE" STORY) 

(DEFPROP F (LIST 'FORWARD (OR (GET 'F 'DEFAULT) 25.)) LISPCODE) 

(DEFPROP F "GOES FORWARD <ARG> OR DEFAULT STEPS" STORY) 

(DEFPROP B (LIST 'BACK (OR (GET 'B 'DEFAULT) 25.)) LISPCODE) 

(DEFPROP B "GOES BACK <ARG> OR DEFAULT STEPS" STORY) 

(DEFPROP R (LIST 'RIGHT (OR (GET 'R 'DEFAULT) 15.)) LISPCODE) 

(DEFPROP R "TURNS RIGHT <ARG> OR DEFAULT DEGREES" STORY) 

(DEFPROP L (LIST 'LEFT (OR (GET 'L 'DEFAULT) 15.)) LISPCODE) 

(DEFPROP L "TURNS LEFT <ARG> OR DEFAULT DEGREES" STORY) 

;;;NOTE PENUP AND PENDOWN ARE PUT IN PROGRAM BUFFER (NOTICE ')

(DEFPROP U '(PENUP) LISPCODE) 

(DEFPROP U "PUTS THE PEN UP" STORY) 

(DEFPROP D '(PENDOWN) LISPCODE) 

(DEFPROP D "PUTS THE PEN DOWN" STORY) 

(DEFPROP C (PROGN (SETQ PROGRAM NIL) (CLEARSCREEN) (PENDOWN)) LISPCODE) 

(DEFPROP C "CLEARS SCREEN AND PROGRAM BUFFER" STORY) 

(DEFPROP A (AGAIN) LISPCODE)

(DEFUN AGAIN NIL
	 (COND ((= NUMBER 0.) (CAAR PROGRAM))
	       (T (DO ((I NUMBER (1- I))
		       (PRGM PROGRAM (CDR PRGM))
		       (BEGINSTATE (HERE) (HERE)))
		       ((= I 0.)
			(EVAL (CAAR PRGM))
			(SETQ PROGRAM (CONS (CONS (CAAR PRGM) BEGINSTATE) PROGRAM))))
		  NIL)))

(DEFPROP
 A
 "DOES THE LAST COMMAND AGAIN OR LAST <ARG> COMMANDS"
 STORY) 

(DEFPROP P (AND (REMEMBERSTATE) NIL) LISPCODE) 

(DEFPROP
 P
 "NAMES AND REMEMBERS THE STATE OF THE TURTLE TO BE USED WITH G"
 STORY) 

(DEFUN REMEMBERSTATE NIL 
       (PUTPROP
	(AND
	 (TOP-SCREEN-TYPE '"WHAT DO YOU WANT TO CALL THIS TURTLE STATE?")
	 (TYPEIN))
	(PRINT (HERE))
	'TURTLESTATE)) 

(DEFPROP G (GOTOSTATE) LISPCODE) 

(DEFPROP G "GOES TO A TURTLE STATE NAMED BY P" STORY) 

;;;HOME INITIALLY DEFINED FOR USE BY "G"

(DEFPROP HOME (0. 0. 0.) TURTLESTATE) 

(DEFUN GOTOSTATE NIL 
       ((LAMBDA (NAME) 
	 (COND
	  ((GET NAME 'TURTLESTATE) (MOVE-TO (GET NAME 'TURTLESTATE)))
	  (T (TOP-SCREEN-TYPE NAME '" IS NOT THE NAME OF A MOUSE STATE")
	     (TERPRI)
	     (TOP-SCREEN-TYPE '"TRY AGAIN"))))
	(AND (TOP-SCREEN-TYPE '"WHAT IS THE NAME OF THE POINT? ")
	     (TYPEIN)))
       NIL) 

(DEFUN MOVE-TO (STATE) 
       ((LAMBDA (POINT NEW-HEADING PENSTATE) 
		(EVAL (CAR (SETQ PROGRAM (CONS (CONS (PRINT '(PENUP)) (HERE))
					       PROGRAM))))
		(EVAL (CAR (SETQ PROGRAM
				 (CONS (CONS (PRINT (LIST 'RIGHT
							  (TOWARDS POINT)))
					     (HERE))
				       PROGRAM))))
		(EVAL (CAR (SETQ PROGRAM
				 (CONS (CONS (PRINT (LIST 'FORWARD
							  (RANGE POINT)))
					     (HERE))
				       PROGRAM))))
		(EVAL (CAR (SETQ PROGRAM
				 (CONS (CONS (PRINT (LIST 'RIGHT
							  (- NEW-HEADING (HEADING))))
					     (HERE))
				       PROGRAM))))
		(AND PENSTATE
		     (PENDOWN)
		     (PRINT '(PENDOWN))
		     (SETQ PROGRAM (CONS (CONS '(PENDOWN) (HERE)) PROGRAM))))
	(LIST (CAR STATE) (CADR STATE))
	(CADDR STATE)
	:PENSTATE)) 

;;;HELP FEATURE

(DEFPROP ? (AND (TALK-ABOUT (READCH)) NIL) LISPCODE) 

(DEFPROP
 ?
 "FOR A SHORT DESCRIPTION TYPE ? FOLLOWED BY A L R F B C H S T ? Q X U D N P G OR W"
 STORY) 

(DEFUN TALK-ABOUT (LETTER) 
       ((LAMBDA (STORY) 
	 (COND (STORY (TERPRI)
		      (TOP-SCREEN-TYPE STORY)
		      (AND (EQ LETTER '?)
			   NEWCOMMANDS
			   (TOP-SCREEN-TYPE '" OR THE NEW COMMANDS "
				 NEWCOMMANDS)))
	       ((GET LETTER 'LISPCODE)
		(TERPRI)
		(TOP-SCREEN-TYPE '"NO INFO AVAILABLE"))
	       (T (TOP-SCREEN-TYPE LETTER '" IS NOT RECOGNIZED"))))
	(GET LETTER 'STORY))) 

;;;NOTE THIS ONE IS A LITTLE SCREWY
;;;IF YOU USE "#" YOU MIGHT GET THE SQUARE OF NUMBER

(DEFPROP W (WAIT-FOR-LINE) LISPCODE)

(DEFUN WAIT-FOR-LINE NIL
	 (AND (PRINC '/ / / / / )
	      ((LAMBDA (GOOD-CODE) (COND ((= NUMBER 0.) GOOD-CODE)
					 (T (LIST 'REPEAT
						  (LIST 'QUOTE GOOD-CODE)
						  NUMBER))))
	       (REQUEST))))

(DEFPROP W
	 "WAITS FOR FULL LOGO STATEMENT AND EXECUTES IT"
	 STORY) 

(DEFPROP T
	 (AND (DEFINE-PROGRAM PROGRAM)
	      (PRINTOUT)
	      (SETQ LASTPROGRAM (APPEND PROGRAM NIL))
	      (SETQ PROGRAM NIL)
	      NIL)
	 LISPCODE) 

(DEFPROP T
	 "TEACHS THE MOUSE THE COMMANDS SINCE LAST TEACH"
	 STORY) 

;;;SEE ABOVE FOR NORMAL RESPONSES TO "N"'S QUESTIONS

(DEFPROP N (AND (DEFINE-NEW-COMMAND) NIL) LISPCODE) 

(DEFPROP N "DEFINES NEW SINGLE LETTER COMMANDS" STORY) 

(DEFUN DEFINE-NEW-COMMAND NIL 
       ((LAMBDA (LETTER) 
	 (PROG NIL 
	       (AND
		(GET LETTER 'LISPCODE)
		(TOP-SCREEN-TYPE LETTER
		      '"ALREADY DEFINED IT "
		      (GET LETTER 'STORY))
		(LOGO-PRINT
		 '"PLEASE TYPE Y IF YOU WANT TO REDEFINE"
		 LETTER)
		(COND ((EQ (READCH) 'Y) (PRINT 'OK))
		      (T (PRINT 'TRY-AGAIN) (RETURN NIL))))
	       (TOP-SCREEN-TYPE '"WHAT LISP CODE SHOULD "
		     LETTER
		     '" BE?")
	       (PUTPROP LETTER (REQUEST) 'LISPCODE)
	       (TOP-SCREEN-TYPE '"WHAT DOES "
		     LETTER
		     '" DO? ")
	       (PUTPROP LETTER (REQUEST) 'STORY)
	       (OR (MEMQ LETTER NEWCOMMANDS)
		   (SETQ NEWCOMMANDS (CONS LETTER NEWCOMMANDS)))
	       (PRINT 'DONE)))
	(AND (TOP-SCREEN-TYPE '"WHAT LETTER?") (READCH)))) 

(DEFPROP X (AND (XEQ-DUMP (AND (PRINT 'NAME?) (TYPEIN))) NIL) LISPCODE) 

(DEFPROP X
	 "RUNS AND FILLS PROGRAM BUFFER WITH MOUSE PROGRAM"
	 STORY) 

;;;WARNING THE FOLLOWING IS LLOGO IMPLEMENTATION DEPENDENT
;;;I.E. WHERE IT GETS THE CODE FROM

(DECLARE (SPECIAL :SIZE)) 

(DEFUN XEQ-DUMP (PROC) 
       (DO ((CODE (OR (CDDDR (CADDR (GET PROC 'EXPR)))
		      (NULL (LOGO-PRINT PROC '" NOT DEFINED")))
		  (CDDR CODE))
	    (:SIZE (AND (GET PROC 'EXPR) (PRINT 'SIZE?) (TYPEIN)))
	    (BEGINSTATE (HERE) (HERE))
	    (THE-LINE NIL))
	   ((NULL CODE))
	   (COND ((= (LENGTH (CAR CODE)) 1.) (EVAL (SETQ THE-LINE (CAR CODE))))
		 ((EQ (CAAR CODE) 'REPEAT)
		  (EVAL (SETQ THE-LINE
			      (LIST (CAAR CODE)
				    (LIST (CAR (CADR (CAR CODE)))
					  (EVAL (SUBST :SIZE
						       ':SIZE
						       (CADR (CADR (CAR CODE))))))
				    (CADDR (CAR CODE))))))
		 (T (EVAL (SETQ THE-LINE
				(LIST (CAAR CODE)
				      (EVAL (SUBST :SIZE
						   ':SIZE
						   (CADR (CAR CODE)))))))))
	   (SETQ PROGRAM (CONS (CONS THE-LINE BEGINSTATE) PROGRAM))
	   (LOGO-PRINT THE-LINE))) 

(DECLARE (SPECIAL *LINE-NO*)) 

;;;THIS IS CALLED BY "T" AND WRITES THE CODE FOR
;;;PROCEDURES PARAMETERIZING CALLS TO FORWARD (I.E. THE SIZE)
;;;ALSO "FORWARD"S IN A ROW ARE COLLASPED
;;;LIKEWISE FOR BACK,RT, AND LT

(DEFUN DEFINE-PROGRAM (PRGM) 
       ((LAMBDA (NAME) 
	 (COND
	  ((NOT (EQ (APPLY 'TO (LIST NAME ':size)) '?))
           ;;TO RETURNS NO VALUE IF SUCCESSFUL, ELSE USER REFUSED REDEFINITION.
	   (TOP-SCREEN-TYPE '"TRY AGAIN")
	   NIL)
	  (T
	   (OR (MEMQ NAME PROCEDURESTAUGHT)
	       (SETQ PROCEDURESTAUGHT (CONS NAME PROCEDURESTAUGHT)))
	   (DO
	    ((CODE (REVERSE (MAPCAR 'CAR PRGM)) (CDR CODE))
	     (TOTAL-FORWARD 0.)
	     (TOTAL-RT 0.)
	     (*LINE-NO* 0.))
	    ((NULL CODE)
	     (COND ((NOT (= TOTAL-FORWARD 0.))
		    (APPLY 'INSERTLINE
			   (LIST (GEN-LINE-NO)
				 (LIST 'FORWARD
				       (LIST 'TIMES
					     TOTAL-FORWARD
					     ':SIZE)))))
		   ((NOT (= TOTAL-RT 0.))
		    (APPLY 'INSERTLINE
			   (LIST (GEN-LINE-NO) (LIST 'RIGHT TOTAL-RT)))))
	     T)
	    (OR (COND ((EQ (CAAR CODE) 'FORWARD)
		       (> (* (CADR (CAR CODE)) TOTAL-FORWARD) 0.))
		      ((EQ (CAAR CODE) 'BACK)
		       (< (* (CADR (CAR CODE)) TOTAL-FORWARD) 0.)))
		(= TOTAL-FORWARD 0.)
		(AND (APPLY 'INSERTLINE
			    (LIST (GEN-LINE-NO)
				  (LIST 'FORWARD
					(LIST 'TIMES
					      TOTAL-FORWARD
					      ':SIZE))))
		     (SETQ TOTAL-FORWARD 0.)))
	    (OR (EQ (CAAR CODE) 'RIGHT)
		(EQ (CAAR CODE) 'LEFT)
		(= TOTAL-RT 0.)
		(AND (APPLY 'INSERTLINE
			    (LIST (GEN-LINE-NO) (LIST 'RIGHT TOTAL-RT)))
		     (SETQ TOTAL-RT 0.)))
	    (COND ((AND (EQ (CAAR CODE) 'FORWARD)
			(OR (> (* (CADR (CAR CODE)) TOTAL-FORWARD) 0.)
			    (= TOTAL-FORWARD 0.)))
		   (SETQ TOTAL-FORWARD (+ TOTAL-FORWARD (CADR (CAR CODE)))))
		  ((AND (EQ (CAAR CODE) 'BACK)
			(OR (< (* (CADR (CAR CODE)) TOTAL-FORWARD) 0.)
			    (= TOTAL-FORWARD 0.)))
		   (SETQ TOTAL-FORWARD (+ TOTAL-FORWARD (* (CADR (CAR CODE)) -1.))))
		  ((OR (EQ (CAAR CODE) 'RIGHT) (EQ (CAAR CODE) 'LEFT))
		   (SETQ TOTAL-RT (+ TOTAL-RT
				     (COND ((EQ (CAAR CODE) 'RIGHT)
					    (CADR (CAR CODE)))
					   (T (* (CADR (CAR CODE)) -1.))))))
		  ((MEMQ (CAAR CODE) PROCEDURESTAUGHT)
		   
		   (APPLY 'INSERTLINE
			  (LIST (GEN-LINE-NO)
				(SUBST (LIST 'TIMES
					     ':SIZE
					     (CADR (CAR CODE)))
				       (CADR (CAR CODE))
				       (CAR CODE)))))
		  ((AND (EQ (CAAR CODE) 'REPEAT)
			(MEMQ (CAR (CADR (CAR CODE)))
			      (APPEND '(FORWARD BACK) PROCEDURESTAUGHT)))
		   (APPLY 'INSERTLINE
			  (LIST (GEN-LINE-NO)
				(LIST (CAAR CODE)
				      (SUBST (LIST 'TIMES
						   ':SIZE
						   (CADR (CADR (CADR (CAR CODE)))))
					     (CADR (CADR (CADR (CAR CODE))))
					     (CADR (CAR CODE)))
				      (CADDR (CAR CODE))))))
		  (T (APPLY 'INSERTLINE (LIST (GEN-LINE-NO) (CAR CODE)))))))))
	(AND (PRINT 'NAME?) (TYPEIN)))) 

(DEFUN GEN-LINE-NO NIL (SETQ *LINE-NO* (+ *LINE-NO* 10.))) 

;;;THIS IS CALLED BY CODE THAT HAS A REPEAT FACTOR BY "#"

(DEFUN REPEAT (CODE N) (DO ((I N (1- I))) ((= I 0.) NIL) (EVAL CODE))) 

;;;THIS SAVES NEW COMMANDS INFO AND LIST OF PROCEDURES TAUGHT

(DEFUN SAVE-NEW-COMMANDS (FILENAME) 
       (UREAD MOUSE NOTES)
       (APPLY 'UAPPEND FILENAME)
       (IOC R)
       (AND NEWCOMMANDS
	    (MAPC (FUNCTION (LAMBDA (NC) (PRINT (LIST 'DEFPROP
						     NC
						     (GET NC 'LISPCODE)
						     'LISPCODE))))
		  NEWCOMMANDS)
	    (MAPC (FUNCTION (LAMBDA (NC) (PRINT (LIST 'DEFPROP
						     NC
						     (GET NC 'STORY)
						     'STORY))))
		  NEWCOMMANDS)
	    (PRINT (LIST 'AND
			(GETL 'SM '(EXPR LSUBR))
			(LIST 'SETQ
			      'NEWCOMMANDS
			      (LIST 'UNION
				    (LIST 'QUOTE NEWCOMMANDS)
				    'NEWCOMMANDS)))))
       (AND PROCEDURESTAUGHT
	    (PRINT (LIST 'AND
			(GETL 'SM '(EXPR LSUBR))
			(LIST 'SETQ
			      'PROCEDURESTAUGHT
			      (LIST 'UNION
				    (LIST 'QUOTE PROCEDURESTAUGHT)
				    'PROCEDURESTAUGHT)))))
       (UFILE)) 

;;;BORROWED FROM GAG'S GOBBLE

(DEFUN UNION (X Y) 
       (PROG NIL 
	LOOP (COND ((NULL X) (RETURN Y))
		   ((MEMQ (CAR X) Y))
		   ((SETQ Y (CONS (CAR X) Y))))
	     (SETQ X (CDR X))
	     (GO LOOP))) 

;;;SETS ALARM FOR AUTOSAVING OPTION

(DEFUN SET-ALARM? NIL 
       ((LAMBDA (NMINUTES) 
	 (COND
	  ((AND (NUMBERP NMINUTES) (> NMINUTES 0.))
	   (OR
	    (BOUNDP 'AUTOSAVEFILE)
	    (SETQ 
	     AUTOSAVEFILE
	     (AND
	      (TOP-SCREEN-TYPE '"IN WHAT FILE SHOULD THIS BE SAVED? ")
	      (REQUEST))))
	   (UWRITE)
	   (IOC R)
	   (APPLY 'UFILE AUTOSAVEFILE)
	   (ALARMCLOCK 'TIME (SETQ AUTOSAVESECONDS (TIMES NMINUTES 60.)))
	   (SETQ ALARMCLOCK 'AUTOSAVE))
	  (T (SETQ AUTOSAVESECONDS -1.) (TOP-SCREEN-TYPE 'OK))))
	(COND
	 ((BOUNDP 'AUTOSAVESECONDS) (QUOTIENT AUTOSAVESECONDS 60.))
	 (T
	  (TOP-SCREEN-TYPE
	   '"IF YOU WANT TO HAVE YOUR MOUSE WORKSPACE AUTOMATICALLY SAVED 
PLEASE TYPE IN HOW MANY MINUTES BETWEEN SAVINGS
OR REPLY 0. IF YOU DO NOT WANT AUTOSAVING")
	  (TYPEIN))))) 

(DEFUN AUTOSAVE (TIME) 
       (TOP-SCREEN-TYPE '"AUTO SAVING")
       ((LAMBDA (^W) (APPLY 'SAVE AUTOSAVEFILE)
		     (SAVE-NEW-COMMANDS AUTOSAVEFILE)
		     (AND (EQ (CADR AUTOSAVEFILE) '>)
			  (APPLY 'UKILL (LIST (CAR AUTOSAVEFILE) '<)))
		     (ALARMCLOCK 'TIME AUTOSAVESECONDS))
	T)) 

(SETQ AUTOSAVE 'AUTOSAVE UNION 'UNION)

